plugins {
  id "java"
  id "fabric-loom" version "${fabric_loom_version}"
  id "com.vanniktech.maven.publish" version "${maven_publish_plugin_version}"
}

base {
  archivesName = "${project.mod_id}@${project.minecraft_version}"
}

group = project.maven_group
version = project.mod_version
String project_version = "${project.minecraft_version}-${mod_version}"

sourceSets {
  javadoc {}

  testmod {
    compileClasspath += main.compileClasspath
    runtimeClasspath += main.runtimeClasspath
  }
}


loom {
  createRemapConfigurations(sourceSets.testmod)
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
  modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_version}")

  mappings loom.officialMojangMappings()

  // Test mod dependencies
  testmodImplementation sourceSets.main.output
  modTestmodImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

fabricApi {
  configureDataGeneration((settings) -> {
    settings.client = true
    settings.getOutputDirectory().set(project(":").file("src/testmod/generated"))
  })
}

processResources {
  def expandProps = [
    /* Minecraft */
    "minecraft_version"             : minecraft_version,
    "java_version"                  : java_version,

    /* Fabric */
    "fabric_version"                : fabric_version,
    "fabric_loader_version"         : fabric_loader_version,

    /* Mod */
    "mod_name"                      : mod_name,
    "mod_description"               : mod_description,
    "mod_id"                        : mod_id,
    "mod_version"                   : mod_version,
  ]

  filesMatching(['fabric.mod.json']) {
    expand expandProps
  }

  inputs.properties(expandProps)
}

runDatagen {
  outputs.dir file("src/testmod/generated")
  classpath += sourceSets.testmod.runtimeClasspath
}

java {
  withSourcesJar()
  withJavadocJar()

  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21

  tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 21
  }
}

mavenPublishing {
  coordinates(maven_group, mod_id, project_version)

  pom {
    name = mod_name
    description = mod_description
    inceptionYear = "2025"
    url = "https://github.com/username/mylibrary/"
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }
    developers {
      developer {
        id = "sefacestudios"
        name = "Seface Studios"
        url = "https://github.com/Seface-Studios/"
      }
    }
    scm {
      url = "https://github.com/Seface-Studios/${mod_id}./"
      connection = "scm:git:git://github.com/Seface-Studios/${mod_id}.git"
      developerConnection = "scm:git:ssh://git@github.com/Seface-Studios/${mod_id}..git"
    }
  }
}

/*
publishing {
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/Seface-Studios/extended-fabric-datagen")
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }

  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}
*/
